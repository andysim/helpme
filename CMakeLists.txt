cmake_minimum_required(VERSION 3.11 FATAL_ERROR)

if(POLICY CMP0074)
    cmake_policy(SET CMP0074 NEW)  # 3.12
endif()

################  Options: Overview and Not Otherwise Mentioned  ###############
#
#  <<<  CMake build overview  >>>
#
#    >>> cmake -H. -Bobjdir -DCMAKE_INSTALL_PREFIX=/path/to/helpme/install/dir ...
#    ...
#    -- Generating done
#    -- Build files have been written to: /current/dir/objdir
#
#    >>> cd objdir && make -j`getconf _NPROCESSORS_ONLN`
#    >>> make install
#
#  <<<  Compilers and flags  >>>
#
#    - CMAKE_C_COMPILER "C compiler"
#    - CMAKE_C_FLAGS "Additional C flags"
#    - CMAKE_CXX_COMPILER "C++ compiler"
#    - CMAKE_CXX_FLAGS "Additional C++ flags"
#    - CMAKE_Fortran_COMPILER "Fortran compiler (required for some add-ons)"
#    - CMAKE_Fortran_FLAGS "Additional Fortran flags"
#
#  <<<  Detecting dependencies and add-ons  >>>
#
#    - PYTHON_EXECUTABLE "Python interpreter to use (e.g., /path/to/bin/python2.7)"
#    - SPHINX_ROOT "Root directory for Sphinx: 'bin/sphinx-build' (or similar) should be in this dir."
#

project(helpme
        LANGUAGES C CXX)
set(helpme_URL "http://www.github.com/andysim/helpme")
set(helpme_EMAIL "andrew.simmonett@nih.gov")
set(helpme_LICENSE "BSD-3-clause")
set(helpme_DESCRIPTION "helPME: an efficient library for particle mesh Ewald")

list(APPEND CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)

set (CMAKE_CXX_STANDARD 11)

#
# Language bindings options
#
include(custom_color_messages)
option(ENABLE_Fortran "Enables the building of Fortran bindings (needs Fortran compiler)" ON)
if(ENABLE_Fortran)
    enable_language(Fortran)
    set(Fortran_ENABLED ON)
endif()
option(INSTALL_PYMOD "Additionally installs as independent python module in PYMOD_INSTALL_LIBDIR" OFF)

#
# Build options
#
include(optionsTools)
option_with_print(ENABLE_OPENMP "Enables OpenMP parallelization" ON)
option_with_print(ENABLE_MPI "Enables MPI parallelization" ON)
option_with_flags(ENABLE_CODE_COVERAGE "Enables details on code coverage" OFF
                  "-ftest-coverage -fprofile-arcs -fPIC -O0 -g")
option_with_flags(ENABLE_BOUNDS_CHECK "Enables bounds check in Fortran" OFF
                  "-ftrapuv -check all -fpstkchk" "-fcheck=all" "-fbounds-check -fcheck-array-temporaries")
option_with_flags(ENABLE_ASAN "Enables address sanitizer" OFF
                  "-fsanitize=address" "-fno-omit-frame-pointer")
option_with_flags(ENABLE_MSAN "Enables address sanitizer" OFF
                  "-fsanitize=memory" "-fno-omit-frame-pointer")
option_with_flags(ENABLE_TSAN "Enables thread sanitizer" OFF
                  "-fsanitize=thread" "-fno-omit-frame-pointer -pie")
option_with_flags(ENABLE_UBSAN "Enables undefined behavior sanitizer" OFF
                  "-fsanitize=undefined" "-fno-omit-frame-pointer")
option_with_default(CMAKE_BUILD_TYPE "Build type (Release or Debug)" Release)
option_with_flags(ENABLE_XHOST "Enables processor-specific optimization" ON
                  "-xHost" "-march=native")
option_with_default(BUILD_FPIC "Libraries will be compiled with position independent code" ON)
option_with_print(BUILD_SHARED_LIBS "Build final library as shared, not static" ON)
option_with_default(CMAKE_INSTALL_LIBDIR "Directory to which libraries installed" lib)
option_with_default(PYMOD_INSTALL_LIBDIR "Location within CMAKE_INSTALL_LIBDIR to which python modules are installed
                                          Must start with: / . Used to imitate python install: /python3.6/site-packages ." /)

set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -O0")
set(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} -O0")
set(CMAKE_Fortran_FLAGS_DEBUG "${CMAKE_Fortran_FLAGS_DEBUG} -O0")

# FFTW
find_package(FFTW REQUIRED)

# OpenMP
if(ENABLE_OPENMP)
    find_package(OpenMP MODULE REQUIRED COMPONENTS CXX)
endif()

# MPI
if(ENABLE_MPI)
    find_package(MPI)
endif()

if(MPI_CXX_FOUND)
    message(STATUS "${Cyan}Found MPI: ${MPI_INCLUDE_PATH}${ColourReset}")
    set(HAVE_MPI TRUE)
    add_definitions("-DHAVE_MPI=1")
else()
    if(ENABLE_MPI)
        message(STATUS "${Red}MPI not found${ColourReset}")
    else()
        message(STATUS "${Red}MPI not searched for${ColourReset}")
    endif()
endif()

include(GNUInstallDirs)
# Find Python
if(${INSTALL_PYMOD})
    set(Python_ADDITIONAL_VERSIONS 3.7 3.6 3.5)
    find_package(PythonLibsNew 2.7 REQUIRED)
    message(STATUS "${Cyan}Found Python ${PYTHON_VERSION_MAJOR}.${PYTHON_VERSION_MINOR}${ColourReset}: ${PYTHON_EXECUTABLE} (found version ${PYTHON_VERSION_STRING})")
    add_subdirectory(external/pybind11)
    add_subdirectory(python)
endif()

# Handle different C++ libraries
if(CMAKE_CXX_COMPILER_ID MATCHES "AppleClang")
    set(cpplib c++)
else()
    set(cpplib stdc++)
endif()

# The C++ library is linked explicitly because exception handling on macOS appears to be broken otherwise.
set(EXTERNAL_LIBRARIES fftw::fftw ${cpplib})

# Documentation
if (BUILD_SPHINX)
    find_package(Sphinx)
    find_package(Latex)
endif()
if (BUILD_DOXYGEN)
    find_package(Doxygen)
    if (NOT DOXYGEN_FOUND)
        message (FATAL_ERROR "Doxygen is needed to build the documentation. Please install it correctly.")
    endif()
    if(SPHINX_EXECUTABLE)
        add_subdirectory(docs)
    endif()
endif()

add_subdirectory(src)


#
# Testing
#
enable_testing ()
add_subdirectory (test)

